#!/bin/sh

awk '
BEGIN {
    FS=","
    OFMT="%.02f"
    whitespc = "[ \t\n\r\f\v]"
}

# skip invalid records
NF < 4 || ($3 !~ /^\.*[0-9]+\.*[0-9]*$/) { next }

{
    paid[$1] += $3

    # paid but did not receive
    if (!received[$1]) received[$1] = 0
    
    for (i = 4; i <= NF; i++) {
        received[$i] += $3 / (NF - 3)

        # received but did not pay
        if (!paid[$i]) paid[$i] = 0
    }
}

END {
    for (p in paid) {
        lent[p] = paid[p] - received[p]
        printf("%-11.11s %8.02f\n", p, lent[p]) | "sort"
    }
}
' $*
